apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

description 'Strategy to load TOML v0.4.0 files'
group = 'com.github.ladutsko'
version = '2.0.0'

repositories {
    mavenLocal()
    jcenter()
    maven {
        url 'https://jitpack.io'
    }
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

ext.versions = [
    springBoot: '2.0.3.RELEASE'
]

dependencies {
    compile 'com.github.TheElectronWill:TOML-javalib:v1.2.2'

    compileOnly "org.springframework.boot:spring-boot:${versions.springBoot}"

    testCompile "org.springframework.boot:spring-boot-starter-test:${versions.springBoot}"
}

jar {
    into('META-INF') {
        from files('LICENSE')
    }

    manifest {
        attributes('Implementation-Title' : project.description,
                   'Implementation-Version' : project.version,
                   'Implementation-Vendor' : 'George Ladutsko')
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name project.name
                packaging 'jar'
                description project.description
                url 'https://github.com/ladutsko/spring-boot-starter-toml'

                scm {
                    connection 'scm:git:git@github.com:ladutsko/spring-boot-starter-toml.git'
                    developerConnection 'scm:git:git@github.com:ladutsko/spring-boot-starter-toml.git'
                    url 'git@github.com:ladutsko/spring-boot-starter-toml.git'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'https://github.com/ladutsko/spring-boot-starter-toml/blob/master/LICENSE'
                    }
                }

                developers {
                    developer {
                        id 'ladutsko'
                        name 'George Ladutsko'
                        email 'ladutsko@gmail.com'
                    }
                }
            }
        }
    }
}
